// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

plugins {
    id "com.diffplug.spotless" version "5.3.0"
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url 'https://dl.bintray.com/dkzwm/maven-wmm' }
        google()
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs.add('-Xbootclasspath/p:core/libs/classes.jar')
        }
    }
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    if (it.name != 'app') {
        spotless {
            java {
                target fileTree('.') {
                    include '**/*.java'
                    exclude '**/MaterialProgressDrawable.java'
                }
                googleJavaFormat('1.7').aosp()
                licenseHeaderFile project.rootProject.file('spotless/copyright.java')
            }
        }
    } else {
        spotless {
            java {
                target '**/*.java'
                googleJavaFormat('1.7').aosp()
            }
        }
    }
}

ext {
    android = [
            compileSdkVersion: 28,
            buildToolsVersion: "29.0.3",
            minSdkVersion    : 14,
            targetSdkVersion : 29,
            versionCode      : 76,
            versionName      : "1.7.2"
    ]
    libs = [
            recyclerview: "androidx.recyclerview:recyclerview:1.1.0-beta04",
            viewpager:"androidx.viewpager:viewpager:1.0.0",
            design: "com.google.android.material:material:1.1.0-beta01",
            appcompatV7: "androidx.appcompat:appcompat:1.1.0",
            interpolator: "androidx.interpolator:interpolator:1.0.0",
            annotation: "androidx.annotation:annotation:1.1.0",
            dynamicanimation: "androidx.dynamicanimation:dynamicanimation:1.1.0-alpha03",
    ]
    bintray = [
            siteUrl       : 'https://github.com/dkzwm/SmoothRefreshLayout',
            gitUrl        : 'https://github.com/dkzwm/SmoothRefreshLayout.git',
            groupId       : "com.github.dkzwm",
            libVersion    : android.versionName,
            developerId   : "dkzwm",
            developerName : "Zhang Weiming",
            developerEmail: "aidkzwm@gmail.com",
            licenseName   : "MIT License",
            licenseUrl    : "https://opensource.org/licenses/MIT",
    ]
    allLicenses = ['MIT']
}

